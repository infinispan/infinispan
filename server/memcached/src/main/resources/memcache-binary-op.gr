// Memcached binary protocol
namespace mc;

// Target class
class org.infinispan.server.memcached.binary.BinaryOpDecoderImpl extends BinaryOpDecoder;

// static final field values are literals
constants org.infinispan.server.memcached.binary.BinaryConstants;

// methods vInt, vLong, array, byte...
intrinsics org.infinispan.server.memcached.binary.BinaryIntrinsics;

import java.time.Instant;
import java.time.temporal.Temporal;
import javax.security.auth.Subject;
import org.infinispan.server.memcached.MemcachedServer;

init {
   private boolean deadEnd = false;

   public BinaryOpDecoderImpl(MemcachedServer server, Subject subject) {
      super(server, subject);
   }

   public BinaryOpDecoderImpl(MemcachedServer server) {
      this(server, ANONYMOUS);
   }
}

exceptionally {
   log.trace("Parsing error", t);
   state = 0;
}

deadend {
   if (!deadEnd) {
      log.tracef("Invalid state of parsing");
      deadEnd = true;
   }
   state = 0;
}

// this is the root
root request
   : magic { if (accessLogging) requestStart = Instant.now(); }
     header { if (log.isTraceEnabled()) log.tracef("Parsed header: %s", header); }
     parameters
   ;

header returns BinaryHeader
   : { magic != MAGIC_REQ }? { throw new IllegalStateException("Error reading magic byte or message id: " + magic); }
   | { deadEnd = false } op keyLength extrasLength dataType vbucketId totalBodyLength opaque cas valueLength
     { new BinaryHeader(requestStart, requestBytes, principalName, key, opCode, opaque, cas) }
   ;

magic: byte;
op: opCode;
keyLength: short;
extrasLength: byte;
dataType: byte;
vbucketId: short;
totalBodyLength: int;
opaque: int;
cas: long;
delta: long;
initial: long;
verbosity: int;

// extras
flags: int;
expiration returns int
   : { extrasLength > 0 }? int
   | { 0 }
   ;

valueLength returns int
   : { totalBodyLength - keyLength - extrasLength };

key returns byte[]
   :  { keyLength > 0 }? fixedArray[keyLength]
   |  { org.infinispan.commons.util.Util.EMPTY_BYTE_ARRAY }
   ;

value returns byte[]
   : { valueLength > 0 }? fixedArray[valueLength]
   |  { org.infinispan.commons.util.Util.EMPTY_BYTE_ARRAY }
   ;

parameters switch op
// Operations
   : { GET }? key { get(header, key, false) }
   | { GETQ }? key { get(header, key, true) }
   | { GETK }? key { get(header, key, false) }
   | { GETKQ }? key { get(header, key, true) }
   | { SET }? flags expiration key value { set(header, key, value, flags, expiration, false) }
   | { SETQ }? flags expiration key value { set(header, key, value, flags, expiration, true) }
   | { ADD }? flags expiration key value { add(header, key, value, flags, expiration, false) }
   | { ADDQ }? flags expiration key value { add(header, key, value, flags, expiration, true) }
   | { REPLACE }? flags expiration key value { replace(header, key, value, flags, expiration, false) }
   | { REPLACEQ }? flags expiration key value { replace(header, key, value, flags, expiration, true) }
   | { DELETE }? key { delete(header, key, false) }
   | { DELETEQ }? key { delete(header, key, true) }
   | { INCREMENT }? delta initial expiration key { increment(header, key, delta, initial, expiration, false) }
   | { INCREMENTQ }? delta initial expiration key { increment(header, key, delta, initial, expiration, true) }
   | { DECREMENT }? delta initial expiration key { increment(header, key, -delta, initial, expiration, false) }
   | { DECREMENTQ }? delta initial expiration key { increment(header, key, -delta, initial, expiration, true) }
   | { QUIT }? { quit(header, false) }
   | { QUITQ }? { quit(header, true) }
   | { FLUSH }? expiration { flush(header, expiration, false) }
   | { FLUSHQ }? expiration { flush(header, expiration, true) }
   | { NO_OP }? { noop(header) }
   | { VERSION }? { version(header) }
   | { APPEND }? key value { append(header, key, value, false) }
   | { APPENDQ }? key value { append(header, key, value, true) }
   | { PREPEND }? key value { prepend(header, key, value, false) }
   | { PREPENDQ }? key value { prepend(header, key, value, true) }
   | { VERBOSITY }? verbosity { verbosityLevel(header, verbosity) }
   | { TOUCH }? expiration key { touch(header, key, expiration) }
   | { GAT }? expiration key { gat(header, key, expiration, false) }
   | { GATQ }? expiration key { gat(header, key, expiration, true) }
   | { GATK }? expiration key { gat(header, key, expiration, false) }
   | { GATKQ }? expiration key { gat(header, key, expiration, true) }
   | { STAT }? key { stat(header, key) }
   | { CONFIG_GET }? key { config(header, key) }
// Unknown
   | { throw new IllegalArgumentException("Unknown operation " + op); }
   ;
